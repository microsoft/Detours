CMAKE_MINIMUM_REQUIRED(VERSION 3.10)
PROJECT(detours LANGUAGES C CXX)

MACRO(GET_WIN32_WINNT version)
    IF(WIN32 AND CMAKE_SYSTEM_VERSION)
		SET(ver ${CMAKE_SYSTEM_VERSION})
		STRING(REGEX MATCH "^([0-9]+).([0-9])" ver ${ver})
		STRING(REGEX MATCH "^([0-9]+)" verMajor ${ver})
		# Check for Windows 10, b/c we'll need to convert to hex 'A'.
		IF("${verMajor}" MATCHES "10")
			SET(verMajor "A")
			STRING(REGEX REPLACE "^([0-9]+)" ${verMajor} ver ${ver})
		ENDIF("${verMajor}" MATCHES "10")
		# Remove all remaining '.' characters.
		STRING(REPLACE "." "" ver ${ver})
		# Prepend each digit with a zero.
		STRING(REGEX REPLACE "([0-9A-Z])" "0\\1" ver ${ver})
		SET(${version} "0x${ver}")
    ENDIF()
ENDMACRO()

SET(LIB_MAJOR_VERSION "4")
SET(LIB_MINOR_VERSION "0")
SET(LIB_PATCH_VERSION "1")
SET(LIB_VERSION_STRING "${LIB_MAJOR_VERSION}.${LIB_MINOR_VERSION}.${LIB_PATCH_VERSION}")

# compile in release with debug info mode by default
IF(NOT CMAKE_BUILD_TYPE)
    set(CMAKE_BUILD_TYPE "RelWithDebInfo" CACHE STRING "Choose the type of build, options are: Debug Release RelWithDebInfo MinSizeRel." FORCE)
ENDIF()

INCLUDE_DIRECTORIES("${CMAKE_CURRENT_LIST_DIR}/src")

SET(DETOURS_COMPILE_DEFINITIONS)
SET(DETOURS_COMPILE_OPTIONS)

IF(ENABLE_VERBOSELOG)
    LIST(APPEND DETOURS_COMPILE_DEFINITIONS ENABLE_VERBOSELOG)
ENDIF(ENABLE_VERBOSELOG)

IF(WIN32)
	IF(MSVC_VERSION GREATER_EQUAL 1700)
		LIST(APPEND DETOURS_COMPILE_DEFINITIONS DETOURS_CL_17_OR_NEWER)
	ENDIF(MSVC_VERSION GREATER_EQUAL 1700)
	GET_WIN32_WINNT(ver)
	MESSAGE(STATUS "Windows OS Version: ${ver}")
	IF(ver EQUAL 0x0700)
		LIST(APPEND DETOURS_COMPILE_DEFINITIONS _USING_V110_SDK71_)
		LIST(APPEND DETOURS_COMPILE_DEFINITIONS DETOURS_WIN_7)
	ENDIF(ver EQUAL 0x0700)
	LIST(APPEND DETOURS_COMPILE_DEFINITIONS "_WIN32_WINNT=${ver}")
ELSE(WIN32)
    MESSAGE(FATAL_ERROR "Only Win32 platforms are supported.")
ENDIF(WIN32)

# TODO: What about Windows on ARM, or IA64 systems?
IF("${CMAKE_SIZEOF_VOID_P}" EQUAL "8")
	LIST(APPEND DETOURS_COMPILE_DEFINITIONS "DETOURS_TARGET_PROCESSOR=X64")
	LIST(APPEND DETOURS_COMPILE_DEFINITIONS DETOURS_X64)
	LIST(APPEND DETOURS_COMPILE_DEFINITIONS DETOURS_64BIT)
	LIST(APPEND DETOURS_COMPILE_DEFINITIONS _AMD64_)
ELSE("${CMAKE_SIZEOF_VOID_P}" EQUAL "8")
	LIST(APPEND DETOURS_COMPILE_DEFINITIONS "DETOURS_TARGET_PROCESSOR=X86")
	LIST(APPEND DETOURS_COMPILE_DEFINITIONS DETOURS_X86)
	LIST(APPEND DETOURS_COMPILE_DEFINITIONS _X86_)
ENDIF("${CMAKE_SIZEOF_VOID_P}" EQUAL "8")

LIST(APPEND DETOURS_COMPILE_DEFINITIONS "DETOURS_VERSION=0x4c0c1")
LIST(APPEND DETOURS_COMPILE_DEFINITIONS WIN32_LEAN_AND_MEAN)

IF(MSVC)
	LIST(APPEND DETOURS_COMPILE_DEFINITIONS "_CRT_SECURE_NO_WARNINGS=1")
	SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /EHsc")
ELSE()
	# TODO: Shall we support gcc with MinGW, Cygwin or Clang based building system on Windows?
ENDIF()

ADD_SUBDIRECTORY("src")
ADD_SUBDIRECTORY("samples")
